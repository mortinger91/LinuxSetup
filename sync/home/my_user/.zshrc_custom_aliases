### My zsh common aliases ###

# This function pulls the latest changes from git
# before the `update-config.sh` script runs.
# This is needed in order to run the latest version of the script.
#
# This function is the update entry point, the script is expected to be modified
# while this function is not.
function update_config() {
  if [ "$(whoami)" = "root" ]; then
    echo "This command should not run as root"
    return
  fi
  git -C $CONFIG_DIR switch master >/dev/null 2>&1
  if [ $? -ne 0 ]; then
    echo "git switch failed, master branch not found"
    return
  fi
  BEFORE_PULL=$(git -C $CONFIG_DIR rev-list --count HEAD)
  git -C $CONFIG_DIR pull >/dev/null 2>&1
  if [ $? -ne 0 ]; then
    echo "git pull failed, you have local changes in your working directory or you are not online"
    return
  fi
  AFTER_PULL=$(git -C $CONFIG_DIR rev-list --count HEAD)
  NEW_COMMITS=$((AFTER_PULL - BEFORE_PULL))
  if [ $NEW_COMMITS -gt 0 ]; then
    echo "Pulled $NEW_COMMITS new commits"
  else
    echo "No new changes were pulled"
  fi
  # The script is run even when no updates were fetched from remote
  # because the local configuration could have changed
  $CONFIG_DIR/update-config.sh deps
  # Sourcing .zshrc otherwise the new aliases and changes
  # won't be available in the current terminal after the script has ended.
  #
  # The sourcing cannot happen in the update-config script,
  # because the script is running in a subprocess of the user terminal.
  # This alias is running in the parent process (the same one used by the user).
  source ~/.zshrc
}
alias update-config='update_config'

#-a shows all file, -l long list format, -h human-readable, -F add symbols like / for dirs, --color forces colored output
alias ls='ls -lahF --color=always'

alias cp='cp -i'

alias mv='mv -i'

alias df='df -h'

# Removed since there's a sym link doing the same thing
#alias bat='batcat'

alias grep='rg'

if command -v trash >/dev/null 2>&1; then
  alias rm='trash -v'
else
  alias rm='rm -i'
fi

#-p create all the nested directories
alias mkdir='mkdir -p'

#-a shows all users' processes, -u user-oriented output, -x includes processes without TTY, -f shows process hierarchy (forest)
alias ps='ps auxf'

alias ..='cd ..'

alias xclip='xclip -selection clipboard'

# Try fwupdtool in case of issues with fwupdmgr. Override the new alias in the _local file
alias up='sudo apt update && sudo apt full-upgrade && sudo fwupdmgr update'

function rebaseMasterImpl() {
  currentBranch=$(git branch --show-current)
  echo "Do you want to rebase $currentBranch with current master? (y/n)"
  read -r answer
  if [[ "$answer" = "${answer#[Yy]}" ]]; then
    echo "Skipped the rebase"
    return
  fi
  git fetch origin master:master
  git rebase master
}
alias rebasemaster="rebaseMasterImpl"

alias squash="git commit -a --amend --no-edit"

alias resethard="git fetch && git reset --hard origin/$(git branch --show-current)"

# INTERFACE is defined in ~/.zshrc
function whatsmyip() {
  timeout=3
  # Internal IP Lookup.
  if command -v ip >/dev/null 2>&1; then
    echo -n "Internal IP: "
    ip addr show $INTERFACE | grep "inet " | awk '{print $2}' | cut -d/ -f1
  else
    echo -n "Internal IP: "
    ifconfig $INTERFACE | grep "inet " | awk '{print $2}'
  fi
  echo ""

  # External IP Lookup
  echo -n "External IP: "
  local ip_output ip_exit_code
  ip_output=$(timeout ${timeout}s curl -s ipinfo.io 2>&1)
  ip_exit_code=$?
  if [ $ip_exit_code -eq 0 ]; then
    echo "$ip_output"
  elif [ $ip_exit_code -eq 124 ]; then
    echo "curl timeout"
  else
    echo "curl error: $ip_output"
  fi
  echo ""

  echo -n "DNS: "
  local dns_output dns_exit_code
  dns_output=$(timeout ${timeout}s nslookup google.com 2>&1)
  dns_exit_code=$?
  if [ $dns_exit_code -eq 0 ]; then
    echo "$dns_output" | grep "^Address:" | head -n 1 | awk '{print $2}'
  elif [ $dns_exit_code -eq 124 ]; then
    echo "nslookup timeout"
  else
    echo "nslookup error: $dns_output"
  fi
}
alias myip="whatsmyip"

function fzf_preview() {
  if [ $# -eq 0 ]; then
    fzf --preview "batcat --color=always --style=numbers,header --line-range=:500 {}" --preview-window wrap | xclip -selection clipboard
  elif [ $# -eq 1 ]; then
    find "$1" -type f | fzf --preview "batcat --color=always --style=numbers,header --line-range=:500 {}" --preview-window wrap | xclip -selection clipboard
  else
    echo "Error: Expected 0 or 1 arguments, got $#" >&2
    return 1
  fi
}
alias lsf='fzf_preview'

function git_history() {
  git log -S "$1" --format="%H" | xargs -I {} git show {} | grep -C 3 "$1"
}
alias githist='git_history'

# My zsh system-specific aliases:
if [ -f ~/.zshrc_custom_aliases_local ]; then
  . ~/.zshrc_custom_aliases_local
fi
